       IDENTIFICATION DIVISION.
       PROGRAM-ID. TEMP100.
      *AUTHOR. deps81@gmail.com

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  SENSOR-REC.
           05 SENSOR-ID.
               10 SENSOR-PROT  PIC  X.
               10 SENSOR-NUM   PIC  9(3).
           05 SENSOR-TEMP      PIC +999.9.
           05 SENSOR-HUMID     PIC  999.
           05 SENSOR-TIMESTAMP PIC  9(10).

       01  TMP-TEMP    PIC S999V9.

       01  CHECK-PROTOCOL PIC X(50).
       01  CHECK-MODEL    PIC X(50).
       01  CHECK-ID       PIC 999.
       01  CHECK-TYPE     PIC 9.

       01  TELLSTICK-SENSOR-VARS.
           05 TSV-PROTOCOL     PIC X(50).
           05 TSV-MODEL        PIC X(50).
           05 TSV-DEVICE-ID    PIC 999.
           05 TSV-DATA-TYPE    PIC 9.
           05 TSV-SENSOR-VALUE PIC X(50).
           05 TSV-SENSOR-LEN   PIC 99 VALUE 50.
           05 TSV-TIMESTAMP    USAGE BINARY-LONG UNSIGNED.
           05 TSV-RETURN       PIC S9(4).

       01  TELLSTICK-SENSOR-ITER.
           05 TSI-PROTOCOL     PIC X(50).
           05 TSI-MODEL        PIC X(50).
           05 TSI-ID           USAGE BINARY-CHAR UNSIGNED.
           05 TSI-DATATYPES    USAGE BINARY-LONG UNSIGNED.
           05 TSI-RETURN       USAGE BINARY-CHAR.


       PROCEDURE DIVISION.
           DISPLAY "CONNECTING TO TELLSTICK..."
           CALL "telldusInit".
           DISPLAY "CONNECTING TO DATABASE..."
           CALL "SQLinit".

           PERFORM 200-START-ITERATION.

           CALL "telldusClose".
           CALL "SQLclose".

           STOP RUN.



       200-START-ITERATION.
           DISPLAY "ITERATING SENSORS..."
           MOVE ZERO TO TSI-RETURN.
           PERFORM 201-ITERATE-SENSORS 
             UNTIL TSI-RETURN IS NOT EQUAL 0.

           DISPLAY "DONE".


       201-ITERATE-SENSORS.
           MOVE SPACES TO TELLSTICK-SENSOR-ITER.
           CALL "getSensors" USING
               BY REFERENCE TSI-PROTOCOL BY VALUE LENGTH OF TSI-PROTOCOL
               BY REFERENCE TSI-MODEL BY VALUE LENGTH OF TSI-MODEL
               BY REFERENCE TSI-ID
               BY REFERENCE TSI-DATATYPES
               RETURNING TSI-RETURN.

           IF TSI-RETURN IS EQUAL TO 0
      D      DISPLAY "* GOT SENSOR " TSI-ID
             MOVE TSI-PROTOCOL TO CHECK-PROTOCOL
             MOVE TSI-MODEL TO CHECK-MODEL
             MOVE TSI-ID TO CHECK-ID
     
             MOVE 1 TO CHECK-TYPE
             PERFORM 210-POLLSENSOR

             MOVE TSV-TIMESTAMP TO SENSOR-TIMESTAMP
             MOVE TSI-PROTOCOL(1:1) TO SENSOR-PROT
             MOVE CHECK-ID TO SENSOR-NUM
             MOVE TSV-SENSOR-VALUE TO TMP-TEMP
             MOVE TMP-TEMP TO SENSOR-TEMP
      
             MOVE 2 TO CHECK-TYPE
             PERFORM 210-POLLSENSOR

             MOVE TSV-SENSOR-VALUE TO SENSOR-HUMID

      D      DISPLAY SENSOR-REC
             PERFORM 400-DATABASE-INSERT
           END-IF.


       210-POLLSENSOR.
      *    Setup call
           MOVE CHECK-ID TO TSV-DEVICE-ID.
           MOVE CHECK-PROTOCOL TO TSV-PROTOCOL.
           MOVE CHECK-MODEL TO TSV-MODEL.
           MOVE SPACE TO TSV-SENSOR-VALUE.
      *    1 = TEMPERATURE, 2 = HUMIDITY
           MOVE CHECK-TYPE TO TSV-DATA-TYPE.

      D    DISPLAY "* POLLING SENSOR '" FUNCTION TRIM(CHECK-PROTOCOL) 
      D            "', '" FUNCTION TRIM (CHECK-MODEL)
      D            "', " CHECK-ID " AS " CHECK-TYPE.

           CALL "getSensorValue" USING
               BY REFERENCE TSV-PROTOCOL
               BY REFERENCE TSV-MODEL
               BY VALUE TSV-DEVICE-ID
               BY VALUE TSV-DATA-TYPE
               BY REFERENCE TSV-SENSOR-VALUE
               BY VALUE TSV-SENSOR-LEN
               BY REFERENCE TSV-TIMESTAMP
               RETURNING TSV-RETURN.

       400-DATABASE-INSERT.
           CALL "insertTemperature" USING
              BY REFERENCE SENSOR-ID
              BY REFERENCE SENSOR-TEMP
              BY VALUE SENSOR-HUMID
              BY VALUE SENSOR-TIMESTAMP. 
